* title molecular dynamics for azurin in water(initialize the structure)
*

if Restart eq 1 goto INI1000

open read card unit 30 name @WaterDir/wcube20.pdb
read sequence pdb unit 30
generate SOLV noangle nodihe setup warn
read coor pdb  unit 30 sele segid SOLV end

coor statistics sele ( segid SOLV ) end

set 1 ?xave
set 2 ?yave
set 3 ?zave

coor trans xdir -@1 ydir -@2 zdir -@3 sele ( segid SOLV ) end

coor statistics sele ( segid SOLV ) end

set 1 ?xave
set 2 ?yave
set 3 ?zave

coor rota xdir @xphi ydir @yphi zdir @zphi phi @phi -
sele ( segid SOLV )  end

coor statistics sele ( segid AZUA ) end
set 4 ?xave
set 5 ?yave
set 6 ?zave

set 7 ?xave
set 8 ?yave
set 9 ?zave

decr 4 by @1
decr 5 by @2
decr 6 by @3

coor trans xdir @4 ydir @5 zdir @6 sele ( segid SOLV ) end
coor statistics sele ( segid SOLV ) end

define HEAVY sele ( segid AZUA .and. ( .not. HYDROGEN ) ) end
define OXYG sele (segid SOLV .and. type OH2) end
delete atom sort sele .byres. ( OXYG .and. -
( HEAVY .around. @OverlayDiam ) ) end

if CryWater eq 0 goto INI3000

  define OVWAT sele ( segid WATE .and. ( .not. HYDROGEN ) ) end
  define OXYG sele (segid SOLV .and. type OH2) end
  delete atom sort sele .byres. ( OXYG .and. -
                          ( OVWAT .around. @OverlayDiam ) ) end
label INI3000
 
define OXYG sele ( segid SOLV .and. type OH2) end

delete atom sort sele .byres. ( OXYG .and. ( .not. ( point -
@7 @8 @9 cut @Radius )  )  )  end


if CryWater eq 1 define OXYC sele ( segid WATE .and. type OH2) end

if CryWater eq 1 delete atom sort sele .byres. ( OXYC .and. -
                 ( .not. ( point @7 @8 @9 cut @Radius )  )  )  end

if Restart eq 0 goto INI2000

label INI1000

open read card unit 30 name water.pdb
read sequence pdb unit 30
generate SOLV noangle nodihe setup warn
read coor pdb  unit 30 sele segid SOLV end

label INI2000

set NAtomBeforeRefill ?NATO

