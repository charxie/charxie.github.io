* title molecular dynamics for azurin in water (setup for dynamics)
*

set Delta     1.00
set ActRad    0.0
set ActRad1   0.0
set ActRad2   0.0
set ActRad3   0.0
set BufRad    0.0

let ActRad3 =  @Radius  - @Delta
let ActRad2 =  @ActRad3 - @Delta
let ActRad1 =  @ActRad2 - @Delta 
let ActRad  =  @ActRad1 - @Delta
let BufRad  =  @ActRad 

if CryWater eq 0 coor statistics sele ( segid SOLV      ) end
if CryWater eq 1 coor statistics sele ( segid SOLV:WATE ) end

coor statistics sele segid AZUA end

set XCenter  ?xave
set YCenter  ?yave
set ZCenter  ?zave

! tag main chain atoms

define MAIN select - 
       ( ( segid AZUA ) .and. -
         ( type C .or. type O .or. type N .or. type HN .or. type CA -
           .or. type S .or. type CU .or. type OT% .or. type HT% )) end

! tag side chain atoms

define SIDE select -
       ( ( segid AZUA ) .and. .not. MAIN ) end 

! tag main chain atoms within reaction zone sphere

define R1 select ( MAIN .and. -
       ( POINT @XCenter @YCenter @ZCenter CUT @ActRad )) end

! tag side chains intersecting reaction zone sphere

define R2 select ( SIDE .and. -
       ( .byres. ( SIDE .and. -
       ( POINT @XCenter @YCenter @ZCenter CUT @ActRad )))) end

! put labels in wcomp

scalar wcomp set 0.0

! reaction zone

define REAC select (R1 .or. R2) end
scalar wcomp set 1.0 select REAC end

! everything else is buffer zone

define BUFF select .not. (resnam TIP3 .or. REAC .or. HYDROGEN) end
scalar wcomp set 2.0 select BUFF end

! assign force constants (xcomp) and friction coefficients (ycomp)
!-----------------------------------------------------------------

! FORCE CONSTANTS
! --------------
!
set TempFactor 0.59
!let TempFactor = @TempFactor * 8.
!let TempFactor = @TempFactor * 3.1416
!let TempFactor = @TempFactor * 3.1416

scalar xcomp set 0.0
!
! store the rms fluctuations in xcomp
! -----------------------------------
scalar xcomp set 0.410 select (BUFF .and. MAIN .and. .not. type H*) end
scalar xcomp set 0.732 select (BUFF .and. SIDE .and. .not. type H*) end
!scalar mass mult 10 select (BUFF .and. .not. type H*) end
!
! use Force constant = m*w**2 = 8(KbT)pi**2/B 
! from: B=8/3 pi**2 < Dr**2>
! and m*w**2 = KbT/<Dr**2>
!
scalar xcomp reciprocal select (BUFF .and. .not. type H*) end
scalar xcomp mult @TempFactor select (BUFF .and. .not. type H*) end
!
! multiply with the appropriate scaling factor depending on the
! atom position in the buffer
!
scalar xcomp mult 0.08 select (BUFF .and. -
     (POINT @XCenter @YCenter @ZCenter CUT @ActRad1)) end
scalar xcomp mult 0.25 select (BUFF .and. -
     (POINT @XCenter @YCenter @ZCenter CUT @ActRad2) .and. .not. -
     (POINT @XCenter @YCenter @ZCenter CUT @ActRad1)) end
scalar xcomp mult 0.42 select (BUFF .and. -
     (POINT @XCenter @YCenter @ZCenter CUT @ActRad3) .and. .not. -
     (POINT @XCenter @YCenter @ZCenter CUT @ActRad2)) end
scalar xcomp mult 0.50 select (BUFF .and. .not. -
     (POINT @XCenter @YCenter @ZCenter CUT @ActRad3)) end

!
! FRICTION COEFFICIENTS
! ---------------------
!
scalar ycomp set 0.0
scalar ycomp set 250. select (BUFF .and. .not. type H*) end

scalar ycomp mult 0.08 select (BUFF .and. -
      (POINT @XCenter @YCenter @ZCenter CUT @ActRad1)) end
scalar ycomp mult 0.25 select (BUFF .and. -
      (POINT @XCenter @YCenter @ZCenter CUT @ActRad2) .and. .not. -
      (POINT @XCenter @YCenter @ZCenter CUT @ActRad1)) end
scalar ycomp mult 0.42 select (BUFF .and. -
      (POINT @XCenter @YCenter @ZCenter CUT @ActRad3) .and. .not. -
      (POINT @XCenter @YCenter @ZCenter CUT @ActRad2)) end    
scalar ycomp mult 0.50 select (BUFF .and. .not. - 
      (POINT @XCenter @YCenter @ZCenter CUT @ActRad3)) end

scalar ycomp set 62.5 select (resnam TIP3 .and. type OH2) end

scalar zcomp = charge  select all end

OPEN WRITE CARD UNIT 41 NAME setup.dat
WRITE COOR COMP CARD UNIT 41

!==============================================================